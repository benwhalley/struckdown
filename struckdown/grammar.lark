?start: (block_comment | system_block | system_append_block | header_block | header_append_block | single_completion | placeholder | templatetag | obliviate | segment_delimiter | markdown)* completion

// Block comments are dropped entirely from the template
// They can contain any content including completions, actions, etc.
block_comment: BLOCK_COMMENT_START BLOCK_COMMENT_CONTENT? BLOCK_COMMENT_END

BLOCK_COMMENT_CONTENT: /(.|\n)+?(?=-->)/

BLOCK_COMMENT_START: "<!--"
BLOCK_COMMENT_END: "-->"

// System message blocks (¡SYSTEM and ¡IMPORTANT are synonyms)
system_block: ("¡SYSTEM" | "¡IMPORTANT") SYSTEM_CONTENT "/END" -> system_replace
system_append_block: ("¡SYSTEM+" | "¡IMPORTANT+") SYSTEM_CONTENT "/END" -> system_append

// Header blocks
header_block: "¡HEADER" HEADER_CONTENT "/END" -> header_replace
header_append_block: "¡HEADER+" HEADER_CONTENT "/END" -> header_append

// Content between tag and /END - captured as terminals to avoid zero-width issues
// Using priority to ensure /END is not captured as content
SYSTEM_CONTENT: /(.|\n)+?(?=\/END)/
HEADER_CONTENT: /(.|\n)+?(?=\/END)/

// markdown: /[^{{\[\¡]+/ -> markdown_text
// using negative lookahead to avoid matching single { or [ or @ (for @@)
// note: <!-- is handled by BLOCK_COMMENT_START terminal which has higher priority
markdown: /(?:(?!{{|\[\[|¡|@|<!--).)+/s  -> markdown_text

placeholder: "{{" WS? var_path WS? "}}"
var_path: CNAME ("." CNAME)*

// Segment delimiters (¡OBLIVIATE and ¡SEGMENT are synonyms)
obliviate: "¡OBLIVIATE" -> obliviate
segment_delimiter: "¡SEGMENT" -> obliviate

?templatetag: /{%\s.*?%}/s        -> templatetag

?completion: single_completion

// Completion syntax: [[type:var|opt1,opt2]] or [[@action:var|opt1,opt2]]
// @ prefix distinguishes actions from LLM completions
single_completion: "[[" WS? completion_body WS? "]]"

completion_body: ACTION_PREFIX CNAME ":" CNAME ("|" option_list)? -> action_call_with_var
    | ACTION_PREFIX CNAME ":" ("|" option_list)? -> action_call_auto_var
    | ACTION_PREFIX CNAME ("|" option_list)? -> action_call_no_var
    | BANG? CNAME quantifier? ":" CNAME ("|" option_list)? -> typed_completion_with_var
    | BANG? CNAME quantifier? ":" ("|" option_list)? -> typed_completion_auto_var
    | BANG? CNAME quantifier? ("|" option_list)? -> typed_completion_no_var

ACTION_PREFIX: "@"
BANG: "!"

quantifier: "*"                           -> zero_or_more
          | "+"                           -> one_or_more
          | "?"                           -> zero_or_one
          | "{" NUMBER "}"                -> exact
          | "{" NUMBER "," "}"            -> at_least
          | "{" NUMBER "," NUMBER "}"     -> between

option_list: option ("," option)*
// allow quoted strings in options
// exclude: comma, ], |
option: STRING | /[^,\]\|]+/
// option: CNAME

%import common.CNAME
%import common.INT -> NUMBER
%import common.ESCAPED_STRING -> STRING
%import common.WS
%ignore WS
